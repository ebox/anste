#!/usr/bin/perl

# Copyright (C) 2007 José Antonio Calvo Fernández <jacalvo@warp.es> 
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License, version 2, as
# published by the Free Software Foundation.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

use strict;
use warnings;

use FindBin qw($Bin);
use lib "$Bin/../lib";

use ANSTE::Manager::Client;

use Getopt::Long;
use Pod::Usage;

use constant PORT => 8666;

my $server = 'localhost';
my $port = PORT;
my $user = `whoami`;
chomp($user);
my $mail = '';
my $help = 0;


GetOptions('server=s' => \$server,
           'port=i' => \$port,
           'user=s' => \$user,
           'mail=s' => \$mail,
           'help' => \$help) or pod2usage(2);

pod2usage(1) if $help;           

pod2usage("$0: No tests given.") if @ARGV == 0;

foreach my $test (@ARGV) {
    addJob($test);
}
exit(0);

sub addJob # (test)
{
    my ($test) = @_;

    my $client = getClient();
    my $ok = $client->addJob($user, $test, $mail);
    if ($ok) {
        print "Job $test sent.\n";
        if ($mail) {
            print "You will be notified at $mail\n";
        } else {
            # TODO: Get this from the server.
            my $url = "http://localhost/anste/$user/feed.xml";
            print "No mail address provided.\n" .
                  "You can subscribe to the results at $url\n"; 
        }
    } else {
        die "Error sending job $test.";
    }
}


sub getClient 
{
    my $URL = "http://$server:$port";

    # Create and return the client object
    my $client = new ANSTE::Manager::Client();
    $client->connect($URL);
    return $client;
}

__END__

=head1 NAME

anste-job - ANSTE job sender

=head1 SYNOPSIS

anste-job [options] [test ...]

 Options:
   -help             shows this help
   -server <host>    host to connect (default: localhost) 
   -port <port>      port to connect (default: 8666)
   -user <username>  remote user (default: your local username)
   -mail <address>   email address if you want notification when job finishes

=cut
