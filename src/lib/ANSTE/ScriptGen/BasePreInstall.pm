# Copyright (C) 2007 José Antonio Calvo Fernández <jacalvo@warp.es> 
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License, version 2, as
# published by the Free Software Foundation.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

package ANSTE::ScriptGen::BasePreInstall;

use strict;
use warnings;

use ANSTE::Scenario::BaseImage;
use ANSTE::Config;
use ANSTE::Exceptions::MissingArgument;
use ANSTE::Exceptions::InvalidType;
use ANSTE::Exceptions::InvalidFile;

# Generates the scripts that installs ansted and anste-slave
# in a base image.

sub new # (image) returns new CommInstallGen object
{
	my ($class, $image) = @_;
	my $self = {};

    defined $image or
        throw ANSTE::Exceptions::MissingArgument('image');

    if (not $image->isa('ANSTE::Scenario::BaseImage')) {
        throw ANSTE::Exceptions::InvalidType('image',
                                            'ANSTE::Scenario::BaseImage');
    }

    $self->{image} = $image;
    my $system = ANSTE::Config->instance()->system();

    eval("use ANSTE::System::$system");
    die "Can't load package $system: $@" if $@;

    $self->{system} = "ANSTE::System::$system"->new();
	
	bless($self, $class);

	return $self;
}

sub writeScript # (file)
{
	my ($self, $file) = @_;

    defined $file or
        throw ANSTE::Exceptions::MissingArgument('file');

    if (not -w $file) {
        throw ANSTE::Exceptions::InvalidFile('file');
    }

    my $system = $self->{system};

	print $file "#!/bin/sh\n";
	my $host = $self->{image}->name();
	print $file "\n# Installation script for ansted and anste-slave\n";
	print $file "# Generated by ANSTE for host $host\n\n"; 

    print $file "# TODO: Check arguments\n\n";

    print $file "# Receives the mount point of the image as an argument\n";
    print $file 'MOUNT=$1'."\n\n";

    $self->_writeCopyFiles($file);
    $self->_writeHostsConfig($file);

    my $config = ANSTE::Config->instance();
    my $port = $config->masterPort();
    my $masterIP = '192.168.45.139'; # FIXME
    my $MASTER = "http://$masterIP:$port";

    print $file "# Stores the master address so anste-slave can read it\n";
    my $command = $system->storeMasterAddress($MASTER);
    print $file "$command\n";
}

sub _writeCopyFiles # (file)
{
    my ($self, $file) = @_;

    my $system = $self->{system};

    print $file "# Copy necessary files\n";
    my $commPath = '/usr/local/lib/site_perl/ANSTE/Comm/';
    my $command = $system->createMountDirCommand($commPath);
    print $file "$command\n";
    my $excPath = '/usr/local/lib/site_perl/ANSTE/Exceptions/';
    $command = $system->createMountDirCommand($excPath);
    print $file "$command\n";

    my %fileDest = ('bin/ansted' => '/usr/local/bin/',
                    'bin/anste-slave' => '/usr/local/bin',
                    'lib/ANSTE/Comm/*.pm' => $commPath,
                    'lib/ANSTE/Exceptions/*.pm' => $excPath,
                    'scripts/ansted' => '/etc/init.d/');                 
    
    foreach my $orig (keys %fileDest) {
        $command = $system->copyToMountCommand($orig, $fileDest{$orig});
        print $file "$command\n";
    }
    print $file "\n";
}

sub _writeHostsConfig # (file)
{
    my ($self, $file) = @_;

    my $system = $self->{system};
    my $host = $self->{image}->name();

    print $file "# Write hosts configuration\n";
    my $config = $system->hostsConfig($host);
    print $file "$config\n\n";
}

1;
